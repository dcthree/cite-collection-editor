import org.gradle.plugins.javascript.coffeescript.CoffeeScriptCompile
import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'coffeescript-base'

group = "edu.harvard.chs"
version = '0.1.0'

repositories {
  mavenCentral()
  mavenRepo name: "Gradle", url: "http://repo.gradle.org/gradle/repo"
  maven {
    url "http://beta.hpcc.uh.edu/nexus/content/repositories/releases"
  }
}

configurations {
  schemas {
    description = 'Manage schemas'
    transitive = true
  }
}

dependencies {
  schemas group: 'edu.harvard.chs', name: 'cite', version: '0.12.20', classifier: 'schemas', ext: 'zip'
}

task compileCoffee(type: CoffeeScriptCompile) {
  source fileTree(coffeesrc)
  include "**/*.coffee"
  destinationDir file("${buildDir.path}/cce/js")
}

task filter(type: Copy) {
  from 'src'
  into "${buildDir.path}/cce"
  include "index.html"
  expand(gradle.properties)
  filter(ReplaceTokens, tokens: [google_client_id: google_client_id, capabilities_url: capabilities_url])
}

task copy(type: Copy) {
  from 'src'
  into "${buildDir.path}/cce"
  include "**/*.html"
  include "**/*.js"
  include "**/*.coffee"
  include "**/*.css"
  include "**/*.xml"
  include "**/*.png"
  include "**/*.swf"
  exclude "index.html"
  exclude "js/collection-editor.js"

  from({zipTree(configurations.schemas.singleFile)}) {
    into 'capabilities'
    include 'collections/CiteCollection.rng'
    eachFile { FileCopyDetails fcp ->
      if (fcp.relativePath.pathString.startsWith('capabilities/collections/')) {
        fcp.relativePath = new RelativePath(true, "capabilities", fcp.relativePath.segments[-1])
      } else {
        fcp.exclude()
      }
    }
  }
}

task build {
  dependsOn compileCoffee, filter, copy
  description = "Assemble website from source"
}


task buildZip(type: Zip, dependsOn: build) {
    from "${buildDir.path}/cce"
    description = 'Builds zip file of java/groovy docs'
}


artifacts {
    archives buildZip
}
